@@grammar :: snk

@@eol_comments :: /#.*?$/
@@whitespace :: /[\t ]+/

spec =
    {nl}
    "lang" lang:text nl
    declare:{decl}
    "net" net:text ":" nl
        nodes:{trans|place}+
    {nl}$
    ;

decl =
    {nl} "declare" ['@' level:text] source:text nl
    ;

place =
    {nl} "place" name:text [type:placetype] ["=" tokens:(","%{ token }+)] nl
    ;

placetype =
    | tuple:placetuple
    | text:text
    ;

placetuple =
    "(" ","%{ placetuple | text }+ ")"
    ;

token =
    | number:number
    | text:text
    ;

trans =
    {nl} "trans" name:text [guard:/[^:]+/] ":" nl
    arcs:{arc}+
    ;

arc =
    {nl} way:("<" | ">") place:text [mod:arcmod] kind:arckind "=" label:tail nl
    ;

arckind = "val" | "var" | "expr" | "flush+" | "flush" | "fill" | tuple
    ;

arcmod =
    | kind:"?"
    | kind:"!" [guard:code]
    ;

tuple =
    "(" ","%{ tuple | text | number }+ ")"
    ;

tailtuple =
    tuple $
    ;

tail =
    /.*?$/
    ;

number =
    /[+-]?[0-9]+/
    ;

nl =
    /\s*[\n\r]\s*/
    ;

text = 
    | code:code
    | name:name
    | string:string
    ;

code =
    | '{' CODEC '}'
    | '[' CODEB ']'
    ;

CODEC =
    { /([^{}]|\\[{}])*/ | CODEC }
    ;

CODEB =
    { /([^\[\]]|\\[\[\]])*/ | CODEB }
    ;

name =
    /[a-zA-Z_][a-zA-Z0-9_]*/
    ;

string =
    | "'''" { /[^'\\]+/ | "''" !"'" | "'" !"'" | /\\(.|\n)/ } "'''"
    | '"""' { /[^"\\]+/ | '""' !'"' | '"' !'"' | /\\(.|\n)/ } '"""'
    | "'" { "\\" /./ | /[^\\\r\n\f']+/ } "'"
    | '"' { "\\" /./ | /[^\\\r\n\f"]+/ } '"'
    ;

